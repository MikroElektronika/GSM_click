'
'Example for GSM Click
'
'    Date          : Nov 2017.
'    Author        : MikroE Team
'
'Test configuration PIC :
'    
'    MCU             : P18F87K22
'    Dev. Board      : EasyPIC PRO v7
'    PIC Compiler ver : v7.1.0.0
'
'---
'
'Description :
'
'The application is composed of three sections :
'
'- System Initialization - Initializes all necessary GPIO pins, UART used for
'the communcation with GSM module and UART used for infromation logging
'- Application Initialization - Initializes driver, power on module and sends few
'command for the default module configuration
'- Application Task - running in parallel core state machine and and checks for call flag.
'If call detected parser will hang up call.
'
'Additional Functions :
'
'All additional functions such as timer initialization and default handler. 
''
'Notes :
'
'- Echo must be turned off for succefull parsing. ( ATE0 )
'- UART polling works much better with HFC enabled.
'- In case of usage of other MCUs Timer initialization must be adjusted according to your MCU.
'
'
program Click_GSM_PIC

include Click_GSM_types
include Click_GSM_config
include Click_GSM_timer

dim
    ' COMMANDS
    GSM_AT as string[3]
    GSM_HANGUP as string[4]
    GSM_ECHO_OFF as string[5]
    GSM_FORMAT_DEFAULT as string[10]
    GSM_HFC_ENABLE as string[11]

    ' CALLER FLAG
    callFlag as uint8_t

sub procedure gsm_default_handler(dim rsp as ^uint8_t, dim evArgs as ^uint8_t) 
dim
    ptr_rsp as ^char
    
    ptr_rsp = rsp + 2
    
    if ( ptr_rsp^ = 82 ) then
        callFlag = 1
    end if

    mikrobus_logWrite( ptr_rsp - 2, _LOG_TEXT )
    
end sub

sub procedure systemInit() 

    GSM_AT = "AT"
    GSM_HANGUP = "ATH"
    GSM_ECHO_OFF = "ATE0"
    GSM_FORMAT_DEFAULT = "AT+CMGF=1"
    GSM_HFC_ENABLE = "AT+IFC=2,2"

    callFlag = 0

    mikrobus_gpioInit( _MIKROBUS1, _MIKROBUS_AN_PIN, _GPIO_INPUT )
    mikrobus_gpioInit( _MIKROBUS1, _MIKROBUS_PWM_PIN, _GPIO_INPUT )
    mikrobus_gpioInit( _MIKROBUS1, _MIKROBUS_INT_PIN, _GPIO_INPUT )
    mikrobus_gpioInit( _MIKROBUS1, _MIKROBUS_RST_PIN, _GPIO_OUTPUT )
    mikrobus_gpioInit( _MIKROBUS1, _MIKROBUS_CS_PIN, _GPIO_OUTPUT )    

    mikrobus_uartInit(_MIKROBUS1, @_GSM_UART_CFG[0]) 
    mikrobus_logInit(_MIKROBUS3, 9600) 

end sub

sub procedure applicationInit() 

' TIMER INIT
    gsm_configTimer()

' DRIVER INIT
    gsm_uartDriverInit(T_GSM_P(@_MIKROBUS1_GPIO), T_GSM_P(@_MIKROBUS1_UART)) 
    gsm_coreInit(@gsm_default_handler, 1500)

' MODULE POWER ON
    gsm_hfcEnable(true) 
    gsm_modulePower(true) 

' MODULE INIT
    gsm_cmdSingle(@GSM_AT[0]) 
    gsm_cmdSingle(@GSM_AT[0]) 
    gsm_cmdSingle(@GSM_AT[0]) 
    gsm_cmdSingle(@GSM_ECHO_OFF[0]) 
    gsm_cmdSingle(@GSM_HFC_ENABLE[0]) 
    gsm_cmdSingle(@GSM_FORMAT_DEFAULT[0]) 

end sub

sub procedure applicationTask() 

    gsm_process() 

    if ((0 <> callFlag)) then 
    
        gsm_cmdSingle(@GSM_HANGUP[0]) 
        Delay_ms(3000) 

        callFlag = 0 
    end if

end sub

main :

    systemInit() 
    applicationInit() 
    while (1) 
        applicationTask() 
    wend

end.